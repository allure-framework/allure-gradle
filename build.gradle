group = 'io.qameta.allure'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'ru.vyarus:gradle-quality-plugin:2.2.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'java-gradle-plugin'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = 'UTF-8'
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile localGroovy()
    testCompile gradleTestKit()
    testCompile 'org.testng:testng:6.8'
    testCompile 'commons-io:commons-io:2.5'
    testCompile 'org.assertj:assertj-core:3.6.2'
    testRuntime files(createClasspathManifest)
}

apply plugin: 'ru.vyarus.quality'

quality {
    pmdVersion = '5.5.4'
    checkstyleVersion = '7.6'
    findbugsVersion = '3.0.1'
    codenarcVersion = '0.26.0'
}

test {
    useTestNG(){
        suites 'src/test/resources/testng.xml'
    }
}
