apply plugin: 'maven'

install {
    repositories.mavenInstaller {
        customizePom(pom, project)
    }
}

def customizePom(pom, gradleProject) {
    pom.whenConfigured { generatedPom ->
        // eliminate test-scoped dependencies (no need in maven central poms)
        generatedPom.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // sort to make pom dependencies order consistent to ease comparison of older poms
        generatedPom.dependencies = generatedPom.dependencies.sort { dep ->
            "$dep.scope:$dep.groupId:$dep.artifactId"
        }

        // add all items necessary for maven central publication
        generatedPom.project {
            name = gradleProject.description
            description = gradleProject.description
            url = "https://github.com/allure-framework/allure-gradle"
            organization {
                name = "Qameta IO"
                url = "https://qameta.io"
            }
            licenses {
                license {
                    name = 'The Apache Software License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution = 'repo'
                }
            }
            scm {
                url = 'https://github.com/allure-framework/allure-gradle'
                connection = 'scm:git:git://github.com/allure-framework/allure-gradle'
                developerConnection = 'scm:git:git://github.com/allure-framework/allure-gradle'
            }
            developers {
                developer {
                    id = 'ehborisov'
                    name = 'Egor Borisov'
                    email = 'ehborisov@gmail.com'
                }
                developer {
                    id = 'eroshenkoam'
                    name = 'Artem Eroshenko'
                    email = 'eroshenkoam@qameta.io'
                }
            }
            issueManagement {
                system = 'Github Issues'
                url = 'https://github.com/allure-framework/allure-gradle/issues'
            }
        }
    }
}
